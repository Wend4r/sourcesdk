if(NOT SOURCESDK_DIR)
	message(FATAL_ERROR "SOURCESDK_DIR is empty")
endif()

if(NOT SOURCESDK_ENTITY2_DIR)
	message(FATAL_ERROR "SOURCESDK_ENTITY2_DIR is empty")
endif()

set(SOURCESDK_ENTITY2_NAME "entity2")
set(SOURCESDK_ENTITY2_OUTPUT_NAME "${SOURCESDK_ENTITY2_NAME}")

set(SOURCESDK_ENTITY2_COMPILER_OPTIONS
	${SOURCESDK_COMPILER_OPTIONS}
)

set(SOURCESDK_ENTITY2_LINK_OPTIONS
	${SOURCESDK_LINK_OPTIONS}
)

set(SOURCESDK_ENTITY2_COMPILE_DEFINITIONS
	${SOURCESDK_COMPILE_DEFINITIONS}

	_LIB
)

set(SOURCESDK_PUBLIC_ENTITY2_INCLUDE_DIR "${SOURCESDK_PUBLIC_DIR}/entity2")

set(SOURCESDK_ENTITY2_INCLUDE_DIRS
	${SOURCESDK_COMMON_DIR}
	${SOURCESDK_PROTOBUF_SOURCE_DIR}
	${SOURCESDK_DIR}/game/shared
	${SOURCESDK_PUBLIC_ENTITY2_INCLUDE_DIR}
	${SOURCESDK_PUBLIC_DIR}/mathlib
	${SOURCESDK_PUBLIC_DIR}/tier0
	${SOURCESDK_PUBLIC_DIR}/tier1
	${SOURCESDK_PUBLIC_DIR}
)

list(APPEND SOURCESDK_INCLUDE_DIRS
	${SOURCESDK_PUBLIC_ENTITY2_INCLUDE_DIR}
)

set(SOURCESDK_ENTITY2_SOURCE_FILES
	${SOURCESDK_ENTITY2_DIR}/entityidentity.cpp
	${SOURCESDK_ENTITY2_DIR}/entitysystem.cpp
	${SOURCESDK_ENTITY2_DIR}/entitykeyvalues.cpp
)

add_library(${SOURCESDK_ENTITY2_NAME} STATIC ${SOURCESDK_ENTITY2_SOURCE_FILES})
add_library(${PROJECT_NAME}::${SOURCESDK_ENTITY2_NAME} ALIAS ${SOURCESDK_ENTITY2_NAME})

set_target_properties(${SOURCESDK_ENTITY2_NAME} PROPERTIES
	C_STANDARD 17
	C_STANDARD_REQUIRED ON
	C_EXTENSIONS OFF

	CXX_STANDARD 17
	CXX_STANDARD_REQUIRED ON
	CXX_EXTENSIONS OFF
)

set_target_properties(${SOURCESDK_ENTITY2_NAME} PROPERTIES OUTPUT_NAME ${SOURCESDK_ENTITY2_OUTPUT_NAME})

if(WINDOWS)
	if(SOURCESDK_MSVC_RUNTIME_LIBRARY)
		set_target_properties(${SOURCESDK_ENTITY2_NAME} PROPERTIES MSVC_RUNTIME_LIBRARY "MultiThreaded$<$<CONFIG:Debug>:Debug>")
	endif()
elseif(MACOS)
	set_target_properties(${SOURCESDK_ENTITY2_NAME} PROPERTIES OSX_ARCHITECTURES "x86_64")
endif()

target_compile_options(${SOURCESDK_ENTITY2_NAME} PRIVATE ${PLATFORM_COMPILE_OPTIONS} ${SOURCESDK_ENTITY2_COMPILER_OPTIONS})
target_link_options(${SOURCESDK_ENTITY2_NAME} PRIVATE ${PLATFORM_LINK_OPTIONS} ${SOURCESDK_ENTITY2_LINK_OPTIONS})

target_compile_definitions(${SOURCESDK_ENTITY2_NAME} PRIVATE ${PLATFORM_COMPILE_DEFINITIONS} ${SOURCESDK_ENTITY2_COMPILE_DEFINITIONS})
target_include_directories(${SOURCESDK_ENTITY2_NAME} PRIVATE ${PLATFORM_INCLUDE_DIRS} ${SOURCESDK_ENTITY2_INCLUDE_DIRS})
