if(NOT SOURCESDK_DIR)
	message(FATAL_ERROR "SOURCESDK_DIR is empty")
endif()

set(SOURCESDK_COMMON_NAME "common")
set(SOURCESDK_COMMON_OUTPUT_NAME "comm")

set(SOURCESDK_COMMON_COMPILER_OPTIONS
	${SOURCESDK_COMPILER_OPTIONS}
)

set(SOURCESDK_COMMON_LINK_OPTIONS
	${SOURCESDK_LINK_OPTIONS}
)

set(SOURCESDK_COMMON_COMPILE_DEFINITIONS
	${SOURCESDK_COMPILE_DEFINITIONS}

	_LIB
)

set(SOURCESDK_COMMON_INCLUDE_DIR "${SOURCESDK_COMMON_DIR}")

set(SOURCESDK_COMMON_INCLUDE_DIRS
	${SOURCESDK_COMMON_INCLUDE_DIR}
	${SOURCESDK_PUBLIC_DIR}/tier0
	${SOURCESDK_PUBLIC_DIR}/tier1
	${SOURCESDK_PUBLIC_DIR}
)

list(APPEND SOURCESDK_INCLUDE_DIRS
	${SOURCESDK_COMMON_INCLUDE_DIR}
)

set(SOURCESDK_COMMON_SOURCE_FILES
	${SOURCESDK_COMMON_DIR}/steamid.cpp
)

add_library(${SOURCESDK_COMMON_NAME} STATIC ${SOURCESDK_COMMON_SOURCE_FILES})
add_library(${PROJECT_NAME}::${SOURCESDK_COMMON_NAME} ALIAS ${SOURCESDK_COMMON_NAME})

set_target_properties(${SOURCESDK_COMMON_NAME} PROPERTIES
	C_STANDARD 17
	C_STANDARD_REQUIRED ON
	C_EXTENSIONS OFF

	CXX_STANDARD 17
	CXX_STANDARD_REQUIRED ON
	CXX_EXTENSIONS OFF
)

set_target_properties(${SOURCESDK_COMMON_NAME} PROPERTIES OUTPUT_NAME ${SOURCESDK_COMMON_OUTPUT_NAME})

if(WINDOWS)
	if(SOURCESDK_MSVC_RUNTIME_LIBRARY)
		set_target_properties(${SOURCESDK_COMMON_NAME} PROPERTIES MSVC_RUNTIME_LIBRARY "MultiThreaded$<$<CONFIG:Debug>:Debug>")
	endif()
elseif(MACOS)
	set_target_properties(${SOURCESDK_COMMON_NAME} PROPERTIES OSX_ARCHITECTURES "x86_64")
endif()

target_compile_options(${SOURCESDK_COMMON_NAME} PRIVATE ${PLATFORM_COMPILE_OPTIONS} ${SOURCESDK_COMMON_COMPILER_OPTIONS})
target_link_options(${SOURCESDK_COMMON_NAME} PRIVATE ${PLATFORM_LINK_OPTIONS} ${SOURCESDK_COMMON_LINK_OPTIONS})

target_compile_definitions(${SOURCESDK_COMMON_NAME} PRIVATE ${PLATFORM_COMPILE_DEFINITIONS} ${SOURCESDK_COMMON_COMPILE_DEFINITIONS})
target_include_directories(${SOURCESDK_COMMON_NAME} PRIVATE ${PLATFORM_INCLUDE_DIRS} ${SOURCESDK_COMMON_INCLUDE_DIRS})
